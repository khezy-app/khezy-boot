plugins {
    id("checkstyle")
}

checkstyle {
    toolVersion = "13.1.0"
    def checkStyleFileUri = getClass().getResource("/config/checkstyle/checkstyle.xml")
    config = resources.text.fromUri(checkStyleFileUri)
    // Load and pass the suppressions file path as a property
    def suppressionsUri = getClass().getResource("/config/checkstyle/suppressions.xml")
    configProperties = [
            'suppressionFile': resources.text.fromUri(suppressionsUri).asFile().absolutePath
    ]
    showViolations = true
}

tasks.withType(Checkstyle).configureEach {
    reports {
        xml.required = false
        html.required = true
        sarif.required = true
    }
}

checkstyleMain {
    source = fileTree('src/main/java')
}

tasks.withType(Javadoc).configureEach {
    options {
        // This stops the "warning: no comment" and HTML syntax messages.
        addStringOption('Xdoclint:none', '-quiet')
    }
}